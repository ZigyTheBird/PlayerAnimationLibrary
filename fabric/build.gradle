plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    implementation include("net.neoforged:bus:$rootProject.neobus_version")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    runtimeOnly "gg.moonflower:molang-compiler:$rootProject.molang_compiler_version"
    shadowBundle "gg.moonflower:molang-compiler:$rootProject.molang_compiler_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    relocate 'gg.moonflower.molangcompiler', 'com.zigythebird.playeranim.molangcompiler'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishing {
    repositories {
        maven {
            name = "zigythebirdmods"
            url = "https://maven.zigythebird.com/mods"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "com.zigythebird.playeranim"
            artifactId = "playeranim-fabric-$minecraft_version"
            version = rootProject.mod_version
            from components.java
        }
    }
}
