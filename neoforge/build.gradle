plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    //Original Repository: https://github.com/Ocelot5836/molang-compiler
    runtimeOnly 'com.github.ThuverX:molang-compiler:f6ac2a31e9'
    forgeRuntimeLibrary 'com.github.ThuverX:molang-compiler:f6ac2a31e9'
    shadowBundle 'com.github.ThuverX:molang-compiler:f6ac2a31e9'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    relocate 'gg.moonflower.molangcompiler', 'com.zigythebird.playeranim.lib.molangcompiler'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishing {
    repositories {
        maven {
            name = "zigythebirdmods"
            url = "https://maven.zigythebird.com/mods"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "com.zigythebird.playeranim"
            artifactId = "playeranim-neoforge-$minecraft_version"
            version = rootProject.mod_version
            from components.java
        }
    }
}
