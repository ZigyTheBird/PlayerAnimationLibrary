plugins {
    id "com.gradleup.shadow"
}

loom {
    accessWidenerPath = project(":minecraft:common").loom.accessWidenerPath

    runs {
        client {
            client()
            setConfigName('Neoforge Client')
            ideConfigGenerated(true)
        }
    }
}

configurations {
    shadowCommon
    compileClasspath.extendsFrom shadowCommon
    runtimeClasspath.extendsFrom shadowCommon
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    implementation forgeRuntimeLibrary(api(include("com.zigythebird:mochafloats:$rootProject.molang_version"))) {
        transitive = false
    }
    modRuntimeOnly forgeRuntimeLibrary(include("org.javassist:javassist:$rootProject.javassist_version"))

    shadowCommon(project(path: ":minecraft:common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":core")) { transitive = false }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"

    mergeServiceFiles()
}

remapJar {
    atAccessWideners.add(loom.accessWidenerPath.get().asFile.name)

    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

tasks.named("processResources") {
    from project(":minecraft:common").sourceSets.main.resources
}

sourcesJar {
    def commonSources = project(":minecraft:common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

shadow {
    addShadowVariantIntoJavaComponent = false
}

publishing {
    publications {
        mavenNeoForge(MavenPublication) {
            artifactId = "PlayerAnimationLibNeo"
            from components.java
        }
    }
}
