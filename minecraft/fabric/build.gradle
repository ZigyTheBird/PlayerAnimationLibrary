plugins {
    id "com.gradleup.shadow"
}

loom {
    accessWidenerPath = project(":minecraft:common").loom.accessWidenerPath
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    shadowCommon
    compileClasspath.extendsFrom shadowCommon
    runtimeClasspath.extendsFrom shadowCommon
    developmentFabric.extendsFrom shadowCommon
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API
    modApi fabricApi.module("fabric-command-api-v2", rootProject.fabric_api_version)
    modApi fabricApi.module("fabric-resource-loader-v1", rootProject.fabric_api_version)

    implementation api(include("com.zigythebird:mochafloats:$rootProject.molang_version")) {
        transitive = false
    }
    modRuntimeOnly include("org.javassist:javassist:$rootProject.javassist_version")

    shadowCommon(project(path: ":minecraft:common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":core")) { transitive = false }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
    mergeServiceFiles()

    exclude "architectury.common.marker"
}

remapJar {
    injectAccessWidener = true

    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

sourcesJar {
    def commonSources = project(":minecraft:common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

shadow {
    addShadowVariantIntoJavaComponent = false
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = "PlayerAnimationLibFabric"
            from components.java
        }
    }
}
