import org.redlance.dima_dencep.gradle.publish2discord.tasks.PublishDiscordTask
import org.apache.commons.lang3.StringUtils

plugins {
    id "org.redlance.dima_dencep.gradle.PublishToDiscord" version "1.0.3"
    id "dev.architectury.loom" version "1.13-SNAPSHOT" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT" apply false
    id "com.gradleup.shadow" version "9.3.0" apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    base.archivesName = "${rootProject.archives_base_name}${StringUtils.capitalize(project.name)}"
    version = "${rootProject.mod_version}+mc.${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        // Fix issue with lwjgl-freetype not being found on macOS
        maven {
            url = "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
        maven {
            name = "NeoForged"
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            name = "RedlanceMinecraft"
            url = "https://repo.redlance.org/public"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set 21
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                name = "RedlanceMinecraft"
                url = "https://repo.redlance.org/public"
                credentials {
                    username = "ZigyTheBird"
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

tasks.register("publishToDiscord", PublishDiscordTask) {
    url.set(providers.environmentVariable("DISCORD_WEBHOOK"))

    container {
        it.title = "${rootProject.archives_base_name} v${version} is out!"
        it.description = "### Changes:\n${rootProject.file("CHANGELOG.md").getText("UTF-8")}"

        it.thumbnail("https://raw.githubusercontent.com/ZigyTheBird/PlayerAnimationLibrary/refs/heads/main/minecraft/fabric/src/main/resources/icon.png")
        it.timestamp(System.currentTimeMillis())
    }

    links {
        it.fromEnvHack()
    }

    footer = "||<@&1411332060051669062>||"
}
